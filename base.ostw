globalvar define health_set;
playervar define role;
playervar define base_health;
playervar define base_armor;
playervar define base_shield;
playervar define self_move_speed;
playervar define boop;
playervar define hud_ref;
playervar define current_hero;
playervar define tick_rate;
playervar define setup_complete;
playervar define debug_text;
globalvar define debug_mode;
playervar define armor_tracking;
playervar define current_shield_health;
playervar define max_shield_health;
playervar define time_since_damage_shield;

import "ana.ostw";
import "brig.ostw";
import "dva.ostw";
import "lucio.ostw";
import "moira.ostw";
import "rein.ostw";
import "winston.ostw";
import "zarya.ostw";
import "zen.ostw";
rule: "Hud Text"
Event.OngoingGlobal
{
    
    debug_mode = true;
    DisableInspectorRecording();
    Wait(5);
    SetMatchTime(5);
    Wait(7);
    SetMatchTime(1);
    if(debug_mode)
    {
        CreateHudText(AllPlayers(),null,null,"Server Load: {0}\nAverage: {1}\nPeak: {2}".Format([ServerLoad(),ServerLoadAverage(),ServerLoadPeak()]),Location.Right,0,Color.White,Color.White,Color.White,HudTextRev.VisibleToAndString,Spectators.VisibleAlways);
    }
}

rule: "On Death"
Event.OngoingPlayer
{
    ult_charge = 0;
    brig_rally_timer = 0;
}
rule: "Spawn in"
Event.OngoingPlayer
if(HeroOf(EventPlayer()) != current_hero)
{
    base_armor = 0;
    base_health = 0;
    base_shield = 0;
    self_move_speed = 100;
    role = 0;
    tick_rate = 1/62.5;
    SetKnockbackReceived(EventPlayer(),100);
    //remove health pools
    RemoveAllHealthPoolsFromPlayer(EventPlayer());
    MinWait();
    SetMaxHealth(EventPlayer(),100.1);
    MinWait();
    SetMaxHealth(EventPlayer(),100);
    Heal(EventPlayer(),null,10000);
    DestroyHudText(debug_text);
    if(debug_mode)
    {
        debug_text = CreateHudText(EventPlayer(),null,null,"{0}\n{1}\n{2}".Format([$"Current Hero: {HeroOf()}\nStored Hero: {current_hero}\nMax Health: {MaxHealthOfType(EventPlayer(),HealthType.Health)}", $"Max Armor: {MaxHealthOfType(EventPlayer(),HealthType.Armor)}\nMax Shields: {MaxHealthOfType(EventPlayer(),HealthType.Shields)}\nGoal Health: {base_health}", $"Goal Armor: {base_armor}\nGoal Shields: {base_shield}"]),Location.Left,0,Color.White,Color.White,Color.White,HudTextRev.String,Spectators.DefaultVisibility);
    }
    
    //CreateHudText(EventPlayer(),"{0}\n{1}\n{2}".Format([ServerLoad(),ServerLoadAverage(),ServerLoadPeak()]),null,null,Location.Left,0,Color.White,Color.White,Color.White,HudTextRev.String,Spectators.VisibleNever);
    if(HeroOf(EventPlayer()) == Hero.Ana)
    {
        ana_setup();
    }
    else if(HeroOf(EventPlayer()) == Hero.Brigitte)
    {
        brig_setup();
    }
    else if(HeroOf(EventPlayer()) == Hero.Dva)
    {
        dva_setup();
    }
    else if(HeroOf(EventPlayer()) == Hero.Lucio)
    {
        lucio_setup();
    }
    else if(HeroOf(EventPlayer()) == Hero.Moira)
    {
        moira_setup();
    }
    else if(HeroOf(EventPlayer()) == Hero.Reinhardt)
    {
        rein_setup();
    }
    else if(HeroOf(EventPlayer()) == Hero.Winston)
    {
        winston_setup();
    }
    else if(HeroOf(EventPlayer()) == Hero.Zarya)
    {
        zarya_setup();
    }
    else if(HeroOf(EventPlayer()) == Hero.Zenyatta)
    {
        zen_setup();
    }
    current_hero = HeroOf(EventPlayer());
    setup_complete = true;
    armor_tracking = HealthOfType(EventPlayer(),HealthType.Armor);
}

rule: "Inbetween rounds"
Event.OngoingPlayer
if(!IsGameInProgress())
{
    if(setup_complete == true)
    {
        Wait(10);
        setup_complete = false;
        current_hero = null;
    }
}


rule: "Damage on occassion"
Event.OngoingPlayer
if(Health() < MaxHealth())
{
    Damage(EventPlayer(),null,0.01);
    Heal(EventPlayer(),null,0.01);
    Wait(2.4);
    LoopIfConditionIsTrue();
}

rule: "Testing"
Event.OnDamageDealt
{
    SmallMessage(Attacker(),EventDamage());
}

rule: "Armor Tracking and tank headshot"
Event.OnDamageTaken
if(role == 1)
{
    if(EventWasCriticalHit())
    {
        //increase headshot damage

        if(armor_tracking > 0)
        {
            //armor is involved
            Damage(Victim(),Attacker(),((EventDamage()+5)/0.75)-5);
        }
        else
        {
            //no armor
            Damage(Victim(),Attacker(),EventDamage()/0.75);
        }
    }
}

void apply_custom_health() "apply custom health"
{
    if(EventPlayer().IsInSpawnRoom())
    {
        define temp_pos = EyePosition()+FacingDirectionOf()*1;
        define temp_face = FacingDirectionOf();
        define temp_position = PositionOf(EventPlayer());
        StartCamera(EventPlayer(),temp_pos,temp_face,0);
        SetInvisible(EventPlayer(),InvisibleTo.All);
        RemoveAllHealthPoolsFromPlayer(EventPlayer());
        MinWait();
        MinWait();

        Teleport(EventPlayer(),ObjectivePosition(0)+Vector(0,20,0));
        WaitUntil(EventPlayer().IsInSpawnRoom() == false,10);

        SetStatus(EventPlayer(),null,Status.Unkillable,10);
        Damage(EventPlayer(),null,9999999);
        MinWait();

        AddHealthPoolToPlayer(EventPlayer(),HealthType.Health,9999999,true,true);
        AddHealthPoolToPlayer(EventPlayer(),HealthType.Armor,9999999,true,true);
        AddHealthPoolToPlayer(EventPlayer(),HealthType.Shields,9999999,true,true);

        MinWait();
        RemoveAllHealthPoolsFromPlayer(EventPlayer());
        MinWait();

        if (base_health > 0)
        {
            AddHealthPoolToPlayer(EventPlayer(),HealthType.Health,base_health-1,true,true);
        }

        if (base_armor > 0)
        {
            AddHealthPoolToPlayer(EventPlayer(),HealthType.Armor,base_armor,true,true);
        }

        if (base_shield > 0)
        {
            AddHealthPoolToPlayer(EventPlayer(),HealthType.Shields,base_shield,true,true);
        }

        MinWait();

        Teleport(EventPlayer(),temp_position);
        WaitUntil(IsInSpawnRoom(),10);
        SetFacing(EventPlayer(),temp_face);
        MinWait();
        MinWait();
        StopCamera(EventPlayer());
        ClearStatus(EventPlayer(),Status.Unkillable);
        SetInvisible(EventPlayer(),InvisibleTo.None);
    }
    else
    {
        RemoveAllHealthPoolsFromPlayer(EventPlayer());
        MinWait();
        MinWait();

        SetStatus(EventPlayer(),null,Status.Unkillable,1);
        Damage(EventPlayer(),null,9999999);
        MinWait();

        AddHealthPoolToPlayer(EventPlayer(),HealthType.Health,9999999,true,true);
        AddHealthPoolToPlayer(EventPlayer(),HealthType.Armor,9999999,true,true);
        AddHealthPoolToPlayer(EventPlayer(),HealthType.Shields,9999999,true,true);

        MinWait();
        RemoveAllHealthPoolsFromPlayer(EventPlayer());
        MinWait();

        if (base_health > 0)
        {
            AddHealthPoolToPlayer(EventPlayer(),HealthType.Health,base_health-1,true,true);
        }

        if (base_armor > 0)
        {
            AddHealthPoolToPlayer(EventPlayer(),HealthType.Armor,base_armor,true,true);
        }

        if (base_shield > 0)
        {
            AddHealthPoolToPlayer(EventPlayer(),HealthType.Shields,base_shield,true,true);
        }
        ClearStatus(EventPlayer(),Status.Unkillable);
        MinWait();
    }

}
